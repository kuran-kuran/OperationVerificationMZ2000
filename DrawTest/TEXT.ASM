WIDTH_SIZE:
	DB	40
CURSOR_Y:
	DB	0
CURSOR_X:
	DB	0
CURSOR_ADDR:
	DW	0D000h
TEXT_VRAM_LIMIT:
	DW	0D000h + 1000
TEXT_VRAM_SIZE:
	DW	1000


;テキスト横桁数設定
;	LD	A, 40 or 80
WIDTH:
	PUSH	HL
	LD	(WIDTH_SIZE), A
	CP	40
	IN	A, (0E8h)
	JR	Z, WIDTH_40
	OR	020H	; bit5 Hi WIDTH80
	LD	HL, 0D000h + 2000
	LD	(TEXT_VRAM_LIMIT), HL
	LD	HL, 2000
	LD	(TEXT_VRAM_SIZE), HL
	JR	WIDTH_L1
WIDTH_40:
	AND	0CFh	; bit5 Lo WIDTH40
	LD	HL, 0D000h + 1000
	LD	(TEXT_VRAM_LIMIT), HL
	LD	HL, 1000
	LD	(TEXT_VRAM_SIZE), HL
WIDTH_L1:
	OUT	(0E8h),A
	POP	HL
	RET

ENABLE_TEXT_VRAM_ADDR:
	IN	A,(0E8h)
	AND	03Fh
	OR	0C0h
	OUT	(0E8h),A
	RET

DISABLE_VRAM_ADDR:
	IN	A,(0E8h)
	AND	03Fh
	OUT	(0E8h),A
	RET

;文字と背景の色とプライオリティ設定
; LD	A, 文字の色(文字優先: 0〜7, グラフィック優先: 8〜15)
; LD	B, 背景の色(0〜7)
TEXT_COLOR:
	OUT	(0F5h), A
	LD	A, B
	OUT	(0F4h), A
	RET

;表示位置設定
;	LD	D, Y
;	LD	E, X
CURSOR:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A, D
	LD	(CURSOR_Y), A
	LD	A, E
	LD	(CURSOR_X), A
	LD	H, 0
	LD	L, D
	LD	A, (WIDTH_SIZE)
	CP	40
	JR	Z, CURSOR_WIDTH40
	CALL	MUL_HLx80
	JR	CURSOR_L1
CURSOR_WIDTH40:
	CALL	MUL_HLx40
CURSOR_L1:
	LD	D, 0
	ADD	HL, DE
	LD	DE, 0D000h
	ADD	HL, DE
	LD	(CURSOR_ADDR), HL
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

;改行
NEW_LINE:
	LD	A, (CURSOR_Y)
	INC	A
	CP	25
	JP	M, NEW_LINE_L1
	LD	A, 24
	CALL	SCROLL_UP
NEW_LINE_L1:
	LD	D, A
	LD	E, 0
	CALL	CURSOR
	RET

;一文字表示
;	LD	HL, 0で終わる文字列へのアドレス
PRINT_MSG1:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	HL, (CURSOR_ADDR)
	LD	(HL), A
	INC	HL
	; 画面右下にはみ出したら一番しての行の左側に戻す
	PUSH	HL
	LD	BC, (TEXT_VRAM_LIMIT)
	OR	A
	SBC	HL, BC
	POP	HL
	JR	C, PRINT_MSG1_L2
	LD	HL, (TEXT_VRAM_LIMIT)
	LD	B, 0
	LD	A, (WIDTH_SIZE)
	LD	C, A
	OR	A
	SBC	HL, BC
	CALL	SCROLL_UP
PRINT_MSG1_L2:
	LD	(CURSOR_ADDR), HL
	LD	BC, 0D000h
	OR	A
	SBC	HL, BC
	LD	D, 0
	LD	A, (WIDTH_SIZE)
	LD	E, A
	CALL	DIV16
	LD	A, C
	LD	(CURSOR_Y), A
	LD	A, L
	LD	(CURSOR_X), A
	POP	HL
	POP	DE
	POP	BC
	RET

;メッセージを表示
;	LD	HL, 文字列へのアドレス
PRINT_MSG:
	PUSH	HL
PRINT_MSG_L1:
	LD	A, (HL)
	OR	A
	JR	Z, PRINT_MSG_END
	CALL	PRINT_MSG1
	INC	HL
	JR	PRINT_MSG_L1
PRINT_MSG_END:
	POP	HL
	RET

; Aレジスタの内容を16進表示する
PRINT_HEX:
	PUSH	AF
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	AND	00FH
	CALL	PRINT_HEX1
	POP	AF
	PUSH	AF
	AND	00FH
	CALL	PRINT_HEX1
	POP	AF
	RET

; Aレジスタの0〜Fを表示する
PRINT_HEX1:
	PUSH	AF
	PUSH	BC
	PUSH	HL
	CALL	A_TO_HEX
	CALL	PRINT_MSG1
	POP	HL
	POP	BC
	POP	AF
	RET

; テキスト画面全体を上方向にスクロール
SCROLL_UP:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A, (WIDTH_SIZE)
	CP	40
	JR	Z, SCROLL_UP_L1
	CALL	SCROLL_UP_WIDTH80
	JR	SCROLL_UP_L2
SCROLL_UP_L1:
	CALL	SCROLL_UP_WIDTH40
SCROLL_UP_L2:
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

; 40桁のテキスト画面全体を上方向にスクロール
SCROLL_UP_WIDTH40:
	; scroll
	LD	BC, 1000 - 40
	LD	HL, 0D000h + 40
	LD	DE, 0D000h
	LDIR
	; space
	LD	HL, 0D000h + 1000 - 40
	LD	DE, 0D000h + 1000 - 40 + 1
	LD	BC, 40 - 1
	XOR	A
	LD	(HL), A
	LDIR
	RET

; 80桁のテキスト画面全体を上方向にスクロール
SCROLL_UP_WIDTH80:
	; scroll
	LD	BC, 2000 - 80
	LD	HL, 0D000h + 80
	LD	DE, 0D000h
	LDIR
	; space
	LD	HL, 0D000h + 2000 - 80
	LD	DE, 0D000h + 2000 - 80 + 1
	LD	BC, 80 - 1
	XOR	A
	LD	(HL), A
	LDIR
	RET

; 画面消去
CLS:
	LD	A, (WIDTH_SIZE)
	LD	D, A
	LD	E, 25
	LD	A, ' '
	LD	HL, 0D000h
	CALL	DRAW_RECT_TEXT
	LD	DE, 0
	CALL	CURSOR
	RET

; 桁のテキスト画面で指定した文字で矩形を描く
; LD	A, 'A' ; 描画文字
; LD	D, WIDTH
; LD	E, HEIGHT
; LD	HL, POSITION
DRAW_RECT_TEXT_ADD_X:
	DW	0
DRAW_RECT_TEXT:
	PUSH	BC
	PUSH	HL
DRAW_RECT_TEXT_WIDTH:
	PUSH	AF
	LD	A, (WIDTH_SIZE)
	SUB	D
	LD	B, 0
	LD	C, A
	LD	(DRAW_RECT_TEXT_ADD_X), BC
	POP	AF
	LD	B, E
DRAW_RECT_TEXT_1:
	LD	C, D
DRAW_RECT_TEXT_2:
	LD	(HL), A
	INC	HL
	DEC	C
	JR	NZ, DRAW_RECT_TEXT_2
	PUSH	BC
	LD	BC, (DRAW_RECT_TEXT_ADD_X)
	ADD	HL, BC
	POP	BC
	DEC	B
	JR	NZ, DRAW_RECT_TEXT_1
	POP	HL
	POP	BC
	RET
